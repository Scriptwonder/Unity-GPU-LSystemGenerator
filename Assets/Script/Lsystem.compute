// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel Input
#pragma kernel Derivation
#pragma kernel calculateDepth
#pragma kernel linkArray

RWStructuredBuffer<int> _axiomLetters;
int _stringLength;
int _primaryLetters[1000]; int _primaryLetterSize;
int _primaryLettersIdx[1000]; int _primaryLettersIdxSize;
int _derivedLetters[1000]; int _derivedLetterSize;

float _lengthBuffer[100];


RWStructuredBuffer<int> _preScanResBuffer;
RWStructuredBuffer<int> _preScanCorBuffer;
StructuredBuffer<int> _afterScanResBuffer;
RWStructuredBuffer<int> _DeriveBuffer;

RWStructuredBuffer<float3> _posBuffer;
RWStructuredBuffer<float3> _oriBuffer;
RWStructuredBuffer<int> _linkedBuffer;
RWStructuredBuffer<int> _depthBuffer;


[numthreads(1,1,1)]
void Input (uint3 id : SV_DispatchThreadID)
{
    int cur = _axiomLetters[id.x];
    int res = 1;
    int corIdx = -1;
    for (int i = 0; i < _primaryLetterSize; i++) {
        if (_primaryLetters[i] == cur) {
            if (i == _primaryLetterSize - 1) {
                res = _derivedLetterSize - _primaryLettersIdx[i];
            }
            else {
                res = _primaryLettersIdx[i + 1] - _primaryLettersIdx[i];
            }
            corIdx = i;
        }
    }
    _preScanCorBuffer[id.x] = corIdx;
    _preScanResBuffer[id.x] = res;
}

[numthreads(1,1,1)]
void Derivation(uint3 id : SV_DispatchThreadID) 
{
    //actual derivation
    int cur = _axiomLetters[id.x];
    int curIdx = _afterScanResBuffer[id.x];
    if (_preScanCorBuffer[id.x] != -1) {
        int corIdx = _preScanCorBuffer[id.x];
        int deriveIdxStart = _primaryLettersIdx[corIdx];
        int deriveIdxEnd = corIdx == _primaryLetterSize - 1 ? _derivedLetterSize : _primaryLettersIdx[corIdx + 1];
        if (id.x == 0) {//inclusive sum start from next idx
            for (int i = 0; i < curIdx; i++) {
                _DeriveBuffer[i] = _derivedLetters[deriveIdxStart++];
            }
        }
        else {
            for (int i = _afterScanResBuffer[id.x - 1]; i < curIdx; i++) {
                _DeriveBuffer[i] = _derivedLetters[deriveIdxStart++];
            }
        }
    }
    else {
        _DeriveBuffer[curIdx-1] = cur;
    }
}

[numthreads(1,1,1)]
void calculateDepth(uint3 id : SV_DispatchThreadID)
{
    int cur = _DeriveBuffer[id.x];
    float3 pos = float3(0, 0, 0);
    float3 ori = float3(0, 0, 0);
    int depth = 0;
    switch (cur) {

        // ANGLES
        // Angle = 25 degrees
        case 43:    // +
            ori.x += 22.5;//25 means 25 degrees
            break;
        case 45:    // -
            ori.x -= 22.5;
            break;
        case 42:    // *
            ori.y += 22.5;
            break;
        case 47:    // division(/)
            ori.y -= 22.5;
            break;
        case 38:    // &
            ori.z += 22.5;
            break;
        case 94:    // ^
            ori.z -= 22.5;
            break;

        // Angle = 45 degrees
        case 96:    // `
            ori.x += 45;//45 means 45 degrees
            break;
        case 33:    // !
            ori.x -= 45;
            break;
        case 64:    // @
            ori.y += 45;
            break;
        case 35:    // #
            ori.y -= 45;
            break;
        case 36:    // $
            ori.z += 45;
            break;
        case 37:    // %
            ori.z -= 45;
            break;

        // Angle = 180 degree about Y axis
        case 124:   // |
            ori.y += 180;
            break;
        case 92:    // backslash(\)
            ori.y -= 180;
            break;

        // DEPTH
        case 91:    // [
            depth = 1;
            break;
        case 93:    // ]
            depth = -1;
            break;

        default:
            pos.y += _lengthBuffer[cur];
            break;
    }
    _posBuffer[id.x] = pos;
    _oriBuffer[id.x] = ori;
    _depthBuffer[id.x] = depth;
}

[numthreads(1,1,1)]
void linkArray(uint3 id : SV_DispatchThreadID)
{
    //link step with depthBuffer and linkedBuffer
    int cur = _depthBuffer[id.x];
    if (id.x == 0) {
        _linkedBuffer[id.x] = 0;
    }
    else {
        int index = id.x-1;
        while (index > 0 && _depthBuffer[id.x] < _depthBuffer[index]) {
            index--;
        }
        _linkedBuffer[id.x] = index;
    }
}
